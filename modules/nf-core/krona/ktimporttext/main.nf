process KRONA_KTIMPORTTEXT {
    tag "${meta.id}"
    label 'process_single'

    conda "${moduleDir}/environment.yml"
    container "${workflow.containerEngine == 'singularity'
        ? 'https://depot.galaxyproject.org/singularity/krona:2.8.1--pl5321hdfd78af_1'
        : 'biocontainers/krona:2.8.1--pl5321hdfd78af_1'}"

    input:
    tuple val(meta), path(report)

    output:
    tuple val(meta), path('*.html'), emit: html
    tuple val("${task.process}"), val('krona'), eval("ktImportText | grep -Po '(?<=KronaTools )[0-9.]+'"), topic: versions, emit: versions_krona
    path "versions.yml"                                        , emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    ktImportText  \\
        ${args} \\
        -o ${prefix}.html \\
        ${report}
    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        krona: \$(ktImportText.pl 2>&1 | head -2 | tail -1 | sed "s/.*KronaTools //; s/ - .*//")
    END_VERSIONS
    """

    stub:
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    touch ${prefix}.html
    """
}
